# Lenovo G470 battery pacth
# work with ACPIBatteryManeger.kext


# add B1B2 method to handle with 16 bit registers
into method label B1B2 remove_entry;
into definitionblock code_regex . insert
begin
Method (B1B2, 2, NotSerialized) { Return(Or(Arg0, ShiftLeft(Arg1, 8))) }\n
end;

# write to EC Buffers
into method label WE1B parent_label H_EC remove_entry;
into method label WECB parent_label H_EC remove_entry;
into device label EC0 insert
begin
Method (WE1B, 2, NotSerialized)\n
{\n
    OperationRegion(ERAM, EmbeddedControl, Arg0, 1)\n
    Field(ERAM, ByteAcc, NoLock, Preserve) { BYTE, 8 }\n
    Store(Arg1, BYTE)\n
}\n
Method (WECB, 3, Serialized)\n
// Arg0 - offset in bytes from zero-based EC\n
// Arg1 - size of buffer in bits\n
// Arg2 - value to write\n
{\n
    ShiftRight(Add(Arg1,7), 3, Arg1)\n
    Name(TEMP, Buffer(Arg1) { })\n
    Store(Arg2, TEMP)\n
    Add(Arg0, Arg1, Arg1)\n
    Store(0, Local0)\n
    While (LLess(Arg0, Arg1))\n
    {\n
        WE1B(Arg0, DerefOf(Index(TEMP, Local0)))\n
        Increment(Arg0)\n
        Increment(Local0)\n
    }\n
}\n
end;

# 16-bit registers
into device label EC0 code_regex ENIB,\s+16 replace_matched begin NIB0,8,NIB1,8 end;
into device label EC0 code_regex ERIB,\s+16 replace_matched begin RIB0,8,RIB1,8 end;
into device label EC0 code_regex BPV0,\s+16 replace_matched begin PV00,8,PV01,8 end;
into device label EC0 code_regex BDC0,\s+16 replace_matched begin DC00,8,DC01,8 end;
into device label EC0 code_regex BFC0,\s+16 replace_matched begin FC00,8,FC01,8 end;
into device label EC0 code_regex BAC0,\s+16 replace_matched begin AC00,8,AC01,8 end;

# fix 16-bit methods
into method label _TMP code_regex Store\s+\(0x8400,\s+\\_SB.PCI0.LPCB.EC0.ENIB\) replaceall_matched begin \_SB.PCI0.LPCB.EC0.WECB(0x5D,16,0x8400)\n end;
into method label FANG code_regex Store\s+\(Arg0,\s+ERIB\) replaceall_matched begin WECB(0x94,16,Arg0)\n end;
into method label FANW code_regex Store\s+\(Arg0,\s+ERIB\) replaceall_matched begin WECB(0x94,16,Arg0)\n end;
into method label _BST code_regex \(\^\^EC0.BPV0, replaceall_matched begin (B1B2(^^EC0.PV00,^^EC0.PV01), end;
into method label _BIF code_regex \(\^\^EC0.BDC0 replaceall_matched begin (B1B2(^^EC0.DC00,^^EC0.DC01) end;
into method label _BIF code_regex \(\^\^EC0.BFC0 replaceall_matched begin (B1B2(^^EC0.FC00,^^EC0.FC01) end;
into method label _BST code_regex \(\^\^EC0.BAC0 replaceall_matched begin (B1B2(^^EC0.AC00,^^EC0.AC01) end;



